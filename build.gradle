/* Copyright 2018 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.util.Date

plugins {
    id 'groovy'
    id 'application'
    id 'distribution'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-library'
    id 'maven-publish'
}

mainClassName = 'PipelineTester'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

// We build the sourcesJar so the package can be linked to JCenter.
// See https://github.com/bintray/gradle-bintray-plugin
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.ant:ant:1.8.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.10'
    compile 'commons-cli:commons-cli:1.2'
}

group = 'com.informaticsmatters'
version = '2.3.2'
description 'A Squonk service descriptor validator and pipelines tester'

publishing {
    publications {
        pipelineTester(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// Set environment variables for the Bintray account: -
// - BINTRAY_USER
// - BINTRAY_KEY
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['pipelineTester']

    dryRun = false
    publish = true
    override = false

    pkg {
        userOrg = 'informaticsmatters'
        repo = 'Squonk'
        name = 'pipelines-utils'
        desc = project.description
        licenses = ['Apache-2.0']

        labels = ['pipelines',
                  'pipelines tester']

        websiteUrl = 'https://github.com/InformaticsMatters/pipelines-utils'
        issueTrackerUrl = 'https://github.com/InformaticsMatters/pipelines-utils/issues'
        vcsUrl = 'https://github.com/InformaticsMatters/pipelines-utils.git'

        version {
            name = project.version
            released = new Date()
        }
    }
}
