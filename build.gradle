/* Copyright 2018 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Uses the Gradle Bintray Plugin
// (https://github.com/bintray/gradle-bintray-plugin)
// to publish the main Java artifact (our Pipeline Tester) to Bintray.
//
// You will need to set environment variables for the Bintray account
// credentials: -
//
// - BINTRAY_USER
// - BINTRAY_KEY
//
// Then ... set the version number for publishing with ext.ptVersion.
// (you can't normally re-publish a version so its number must change)
//
// Then ... publish with: -
//
// - gradle bintrayUpload

plugins {
    id 'groovy'
    id 'application'
    id 'distribution'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-library'
    id 'maven-publish'
}

// The PipelineTester version.
// The processResources closure sets the version
// string in the 'resources/tester.properties' properties file.
ext.ptVersion = '2.6.3'
mainClassName = 'PipelineTester'

group = 'com.informaticsmatters'
description 'A Squonk service descriptor validator (the PipelineTester)'

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.apache.ant:ant:1.8.2'
    compile 'org.codehaus.groovy:groovy-all:2.4.10'
    compile 'commons-cli:commons-cli:1.2'
}

// Process resource files,
// replacing @version@ values with the ptVersion defined above.
processResources {
    inputs.property "version", project.ptVersion
    filter(org.apache.tools.ant.filters.ReplaceTokens,
           tokens: [version: project.ptVersion])
}

// We build the sourcesJar and javaDoc so the package can be linked to JCenter.
// See https://github.com/bintray/gradle-bintray-plugin
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Standard MavenPublication settings.
// Here we just publish our Java JAR and the source and javadoc.
// Deployment to the external maven repository (Bintray) is handed by
// the bintray closure and the bintray plugin's `bintrayUpload` task.
//
// Once uploaded (for the first time) you then 'link' the pkg.name to jcenter.
// Linking to jcenter can take some time, so wait for at least 4 hours after
// the initial link request before worrying about the lack of email
// confirmation. Once confirmed the package should be on jcenter.
//
// If you see 'HTTP/1.1 401 Unauthorized' when you run `gw bintrayUpload'
// you've probably forgotten to set your Bintray account credentials.
publishing {
    publications {
        pipelineTester(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['pipelineTester']

    dryRun = false
    publish = true
    override = false

    pkg {
        userOrg = 'informaticsmatters'
        repo = 'Squonk'
        name = 'pipelines-utils'
        desc = project.description
        licenses = ['Apache-2.0']

        labels = ['pipelines',
                  'pipelines tester']

        websiteUrl = 'https://github.com/InformaticsMatters/pipelines-utils'
        issueTrackerUrl = 'https://github.com/InformaticsMatters/pipelines-utils/issues'
        vcsUrl = 'https://github.com/InformaticsMatters/pipelines-utils.git'

        version {
            name = project.ptVersion
            released = new Date()
        }
    }
}
